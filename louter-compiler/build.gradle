import org.gradle.internal.jvm.Jvm

apply plugin: 'java'
apply from: '../AndroidCodeQualityConfig/quality.gradle'
apply plugin: 'com.novoda.bintray-release'

dependencies {
  compile project(':louter-annotations')
  compile deps.javapoet
  compile deps.autoservice

  testCompile deps.test.junit
  testCompile deps.test.mockito
  testCompile deps.test.compiletesting
  testCompile files(getRuntimeJar())
  testCompile files(Jvm.current().getToolsJar())
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

def getRuntimeJar() {
  try {
    final File javaBase = new File(System.getProperty("java.home")).getCanonicalFile();
    File runtimeJar = new File(javaBase, "lib/rt.jar");
    if (runtimeJar.exists()) {
      return runtimeJar;
    }
    runtimeJar = new File(javaBase, "jre/lib/rt.jar");
    return runtimeJar.exists() ? runtimeJar : null;
  } catch (IOException e) {
    throw new RuntimeException(e);
  }
}

publish {
  artifactId = "louter-compiler"
  repoName = rootProject.ext.bintray.repoName
  userOrg = rootProject.ext.bintray.userOrg
  groupId = rootProject.ext.bintray.groupId
  publishVersion = rootProject.ext.bintray.publishVersion
  desc = rootProject.ext.bintray.desc
  website = rootProject.ext.bintray.website
  licences = rootProject.ext.bintray.licences
}